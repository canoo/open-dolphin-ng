plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

import org.opendolphin.gradle.GulpTask
import org.opendolphin.gradle.NpmInstallTask

apply plugin : 'java'
apply plugin: 'maven-publish'

group = 'org.open-dolphin'
version = '1.0-RC2B3'

repositories {
    jcenter()
}

ext {
    webjarconfig = [
        staticWebFilesDir : "${projectDir}/gulp_out",
        baseDir: "META-INF/resources/",
        subDir : "webjars/org/opendolphin/${version}"
    ]
}

dependencies {
    //compile 'org.webjars:bootstrap:3.3.1'
}

task callNpmInstall(type: NpmInstallTask) {
}

task buildjs(group: 'js', type: GulpTask, dependsOn: callNpmInstall) {
    commands = ['buildjs']
    outputs.dir file('gulp_out')
}

jar {
    dependsOn buildjs
    from(fileTree(webjarconfig.staticWebFilesDir)) {
        into webjarconfig.baseDir + webjarconfig.subDir
    }
    outputs.file archivePath
}


publishing {
    publications {
        mavenJavascript(MavenPublication) {
            //from components.java
            artifact jar {
            }
        }
    }
}

bintray {
    dryRun = false
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJavascript']
    pkg {
        userOrg = 'opendolphin'
        repo = 'mavenrepo'
        name = 'open-dolphin-ng'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/canoo/open-dolphin-ng'
        version {
            name = project.version
            desc = 'OpenDolphin - angular.js Adapter'
            released  = new Date()
            vcsTag = project.version
            // attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
// Note: call './gradlew publishToMavenLocal' for local install to '~/.m2'

project.afterEvaluate {
    clean.dependsOn cleanBuildjs
}

